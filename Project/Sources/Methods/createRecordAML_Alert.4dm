//%attributes = {}
// createRecordAML_Alert(subject; description ; tablePtr; recordID): returns a AML_AlertID
// this method creates a new record in the AML_Alert table with subject; detailed description; which table it's related to ; which record)

// 
//[AML_Alerts]
//[AML_Alerts];"Entry"
//[AML_Alerts];"ListBox"

C_OBJECT:C1216($1; $ruleObj)
C_OBJECT:C1216($2; $statsObj)

C_TEXT:C284($3; $description)
C_TEXT:C284($4; $customerID)
C_LONGINT:C283($5; $tableNo)
C_TEXT:C284($6; $recordID)

C_TEXT:C284($ruleID; $subject; $alertID)
C_LONGINT:C283($status)

Case of 
	: (Count parameters:C259=0)
		$ruleObj:=New object:C1471
		$statsObj:=New object:C1471
		$subject:="Test"
		$description:="generated by the testing code"
		
		//: (Count parameters=3)
	: (Count parameters:C259=3)
		$ruleObj:=$1
		$statsObj:=$2
		$ruleID:=$1.ruleID
		$subject:=$1.ruleName
		$description:=$1.description+CRLF+$3
		
	: (Count parameters:C259=6)
		$ruleObj:=$1
		$statsObj:=$2
		$ruleID:=$1.ruleID
		$subject:=$1.ruleName
		$description:=$1.description+CRLF+$3
		$customerID:=$4
		$tableNo:=$5  // longint
		$recordID:=$6  // e.g. invoiceID
	Else 
		assertInvalidNumberOfParams(Current method name:C684; Count parameters:C259)
End case 

// only create one alert per ruleID for same table and same reference
QUERY:C277([AML_Alerts:137]; [AML_Alerts:137]tableNo:4=$tableNo; *)
QUERY:C277([AML_Alerts:137]; [AML_Alerts:137]recordID:3=$recordID; *)
QUERY:C277([AML_Alerts:137]; [AML_Alerts:137]ruleID:15=$ruleID)

If (Records in selection:C76([AML_Alerts:137])=0)
	READ WRITE:C146([AML_Alerts:137])
	CREATE RECORD:C68([AML_Alerts:137])
	
	[AML_Alerts:137]date:5:=Current date:C33
	[AML_Alerts:137]branchID:10:=getBranchID
	
	[AML_Alerts:137]UUID:1:=Generate UUID:C1066
	[AML_Alerts:137]alertID:2:=makeAML_AlertID
	[AML_Alerts:137]ruleID:15:=$ruleID
	[AML_Alerts:137]subject:7:=$subject
	[AML_Alerts:137]customerID:12:=$customerID
	[AML_Alerts:137]description:9:=$description
	[AML_Alerts:137]tableNo:4:=$tableNo
	[AML_Alerts:137]recordID:3:=$recordID
	[AML_Alerts:137]status:8:=$status
	
	[AML_Alerts:137]metaData:19:=New object:C1471()
	[AML_Alerts:137]metaData:19.rule:=$ruleObj.toObject()
	
	
	If (True:C214)  // ---- ADD CUSTOMER PROFILE TO METADATA SO WE KNOW THE CURRENT STATE ----
		C_OBJECT:C1216($custObj)
		$custObj:=ds:C1482.Customers.query("CustomerID == :1"; $customerID).first()
		If ($custObj#Null:C1517)
			[AML_Alerts:137]metaData:19.customer:=$custObj.toObject()
		End if 
	End if 
	
	If (True:C214)  // ---- ADD THE STATS OBJECT TO METADATA -------
		// agg_isRuleMatchingThisCustomer <--- needs to pass backk the stats object from agg_isRuleMatchingThisInvoice
		// then pass the stats object to agg_thenApplyRuleActions <--- then this can pass stats object here
		[AML_Alerts:137]metaData:19.stats:=$statsObj  // currently an empty object
	End if 
	
	$alertID:=[AML_Alerts:137]alertID:2
	
	SAVE RECORD:C53([AML_Alerts:137])
	UNLOAD RECORD:C212([AML_Alerts:137])
	READ ONLY:C145([AML_Alerts:137])
End if 
$0:=$alertID

