//%attributes = {}
// createRecordAML_Alert(subject; description ; tablePtr; recordID): returns a AML_AlertID
// this method creates a new record in the AML_Alert table with subject; detailed description; which table it's related to ; which record)

// 
//[AML_Alerts]
//[AML_Alerts];"Entry"
//[AML_Alerts];"ListBox"

C_TEXT:C284($ruleID; $subject; $description; $recordID; $alertID; $customerID; $1; $2; $3; $4; $6)
C_LONGINT:C283($tableNo; $5; $status)

Case of 
	: (Count parameters:C259=0)
		$subject:="Test"
		$description:="generated by the testing code"
		
	: (Count parameters:C259=3)
		$ruleID:=$1
		$subject:=$2
		$description:=$3
		
	: (Count parameters:C259=6)
		$ruleID:=$1
		$subject:=$2
		$description:=$3
		$customerID:=$4
		$tableNo:=$5  // longint
		$recordID:=$6  // e.g. invoiceID
	Else 
		assertInvalidNumberOfParams(Current method name:C684; Count parameters:C259)
End case 

// only create one alert per ruleID for same table and same reference
QUERY:C277([AML_Alerts:137]; [AML_Alerts:137]tableNo:4=$tableNo; *)
QUERY:C277([AML_Alerts:137]; [AML_Alerts:137]recordID:3=$recordID; *)
QUERY:C277([AML_Alerts:137]; [AML_Alerts:137]ruleID:15=$ruleID)

If (Records in selection:C76([AML_Alerts:137])=0)
	READ WRITE:C146([AML_Alerts:137])
	CREATE RECORD:C68([AML_Alerts:137])
	
	[AML_Alerts:137]date:5:=Current date:C33
	[AML_Alerts:137]branchID:10:=getBranchID
	
	[AML_Alerts:137]UUID:1:=Generate UUID:C1066
	[AML_Alerts:137]alertID:2:=makeAML_AlertID
	[AML_Alerts:137]ruleID:15:=$ruleID
	[AML_Alerts:137]subject:7:=$subject
	[AML_Alerts:137]customerID:12:=$customerID
	[AML_Alerts:137]description:9:=$description
	[AML_Alerts:137]tableNo:4:=$tableNo
	[AML_Alerts:137]recordID:3:=$recordID
	[AML_Alerts:137]status:8:=$status
	
	$alertID:=[AML_Alerts:137]alertID:2
	
	SAVE RECORD:C53([AML_Alerts:137])
	UNLOAD RECORD:C212([AML_Alerts:137])
	READ ONLY:C145([AML_Alerts:137])
End if 
$0:=$alertID

